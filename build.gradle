plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/lycanthropy.accesswiderner")
}

repositories {
	maven {
		name "REI, Cloth Config"
		url "https://maven.shedaniel.me/"
	}

	maven {
		name "Modmenu"
		url "https://maven.terraformersmc.com/"
	}

	maven {
		name "Ladysnake"
		url "https://ladysnake.jfrog.io/artifactory/mods"
	}

	maven {
		name "Cardinal Components, Player Ability Lib"
		url "https://ladysnake.jfrog.io/artifactory/mods"
		content {
			includeGroup 'io.github.ladysnake'
			includeGroupByRegex 'io\\.github\\.onyxstudios.*'
		}
	}

	maven {
		name "Reach Entity Attributes"
		url "https://maven.jamieswhiteshirt.com/libs-release"
		content {
			includeGroup "com.jamieswhiteshirt"
		}
	}

	maven {
		name "Jitpack"
		url "https://jitpack.io"
	}

	maven {
		name "GeckoLib"
		url "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
	}

	maven {
		name "Patchouli"
		url "https://maven.blamejared.com"
	}
	maven {
		name "Appleskin"
		url "https://maven.ryanliptak.com/"
	}
}

dependencies {
	minecraft("com.mojang:minecraft:${project.minecraft_version}")
	mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
	modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")

	//Cloth Config
	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude (group: "net.fabricmc.fabric-api") }

	//Modmenu
	modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}") {
		exclude group: "net.fabricmc.fabric-api" }

	//Cardinal Components
	modApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${cc_version}"
	include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${cc_version}"
	modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${cc_version}"
	include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${cc_version}"

	//Pehkui
	modApi("com.github.Virtuoel:Pehkui:${project.pehkui_version}")

	//Pathouli
	modImplementation("vazkii.patchouli:Patchouli:${project.patchouli_version}") { transitive = false }

	runtimeOnly("me.zeroeightsix:fiber:0.23.0-2")

	//Geckolib
	modImplementation "software.bernie.geckolib:geckolib-fabric-${gecko_version}"

	//Appleskin
	modCompileOnly("squeek.appleskin:appleskin-fabric:${appleskin_version}:api") {
		transitive = false
	}

	//REI
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${rei_version}"

	//PlayerAbilityLib
	modImplementation "io.github.ladysnake:PlayerAbilityLib:${playerabilitylib_version}"
	include "io.github.ladysnake:PlayerAbilityLib:${playerabilitylib_version}"

	//Reach Entity Attributes
	modImplementation "com.jamieswhiteshirt:reach-entity-attributes:${reach_entity_attributes_version}"
	include "com.jamieswhiteshirt:reach-entity-attributes:${reach_entity_attributes_version}"

	//Step Height
	modImplementation "com.github.emilyploszaj:step-height-entity-attribute:${step_height_version}"
	include "com.github.emilyploszaj:step-height-entity-attribute:${step_height_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
	}
}
